$ ->
  attempt = 0
  wrong = 0
  word = ""

  ###
    Init Methods
  ###

  initGame = ->
    # bind button to hangman rules
    bindButtons()
    startNewGame()

  # bind all the buttons
  bindButtons = ->
    # bind guess buttons
    $('.guess').click (e) ->
      e.preventDefault()

      # this button has been clicked
      $(this).addClass 'disabled'

      # try this letter
      guessLetter $(this).text()
      
      # check result
      checkWin()
      checkLose()

    # bind give up button
    $('#give-up').click (e) ->
      e.preventDefault()
      revealAnswer()

    # bind new game button
    $('.new-game').click (e) ->
      e.preventDefault()
      startNewGame()
  
  startNewGame = ->
    resetCounters()
    getNewWord()
  
  # get new word
  getNewWord = ->
    # reset guess buttons
    $('.guess').removeClass 'disabled'
    
    # get a new word from server
    $.get '/words/getRandomWord', (data) ->
      word = data.word.toUpperCase()
      caWord = word.split ''
      html = ''
      for c in caWord
        html += '<span class="label label-danger answer" data-letter=' + c + '>'
        if c != '-'
          html += '_'
        else
          html += c
        html += '</span>'
      $('#my-answer').html html
      # $('#bla').text(word)


  ###
    Counters
  ###
  
  increaseCounters = (correct) ->
    attempt++
    wrong++ if not correct
    updateCountersDisplay()

  resetCounters = ->
    attempt = 0
    wrong = 0
    updateCountersDisplay()

  updateCountersDisplay = ->
    $('#attempt').text attempt
    $('#wrong').text wrong


  ###
    the Game
  ###

  # Guess a letter
  guessLetter = (c) ->
    correct = false
    $('.answer').each (i, element) =>
      if c == $(element).data().letter
        $(element).text($(element).data().letter)
        correct = true
    increaseCounters correct

  # check if game is over and if player win or lose
  checkWin = ->
    win = true
    $('.answer').each (i, element) =>
      if $(element).text().match /[_]/
        win = false
    if win == true
      # disable all buttons
      $('.guess').addClass 'disabled'
      # prompt user for winning
      $('#modal-title').html "You WIN"
      $('#modal-answer').html "Answer : " + word
      $('#wingame').modal 'show'

  checkLose = ->
    lose = false
    # game lost if 6 miss
    if wrong > 5
      lose = true
      # disable all buttons
      $('.guess').addClass 'disabled'
      # reveal answer
      revealAnswer()
      # prompt user for losing
      $('#modal-title').html "You Lost"
      $('#modal-answer').html "Answer : " + word
      $('#wingame').modal 'show'
  
  # show answer
  revealAnswer = ->
    $('.answer').each (i, element) =>
      $(element).text($(element).data().letter)


  ###
    Init Page
  ###

  initGame()
  

